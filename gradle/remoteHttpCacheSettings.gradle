/*
 * Copyright 2017 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def remoteCacheUrl = System.getProperty('gradle.cache.remote.url')

if (remoteCacheUrl) {
    ext.isCiServer = System.getenv().containsKey("CI")
    def remoteCacheUsername = System.getProperty('gradle.cache.remote.username')
    def remoteCachePassword =  System.getProperty('gradle.cache.remote.password')
    boolean remotePush = !(System.getProperty('gradle.cache.remote.push') == 'false')
    buildCache {
        local {
            enabled = !isCiServer
        }
        remote(org.gradle.caching.http.HttpBuildCache) {
            url = remoteCacheUrl
            push = isCiServer && remotePush
            if (remoteCacheUsername && remoteCachePassword) {
                credentials {
                    username = remoteCacheUsername
                    password = remoteCachePassword
                }
            }
        }
    }
}


