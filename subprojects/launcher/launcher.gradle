import org.gradle.build.GradleStartScriptGenerator

// Main entry point requires Java 5
sourceCompatibility = javaVersion.java9Compatible ? 1.6 : 1.5

configurations {
    startScriptGenerator
}

dependencies {
    compile project(':baseServices')
    compile project(':jvmServices')
    compile project(':core')
    compile project(':cli')
    compile project(':toolingApi')
    compile project(':native')
    compile project(':logging')

    compile libraries.commons_io
    compile libraries.slf4j_api

    integTestCompile project(':internalIntegTesting')
    // Needed for testing debug command line option (JDWPUtil)
    integTestCompile files(jvmForCompilation.toolsJar ?: [])
    integTestRuntime project(':plugins')
    integTestRuntime project(':languageNative')

    testFixturesCompile project(':internalIntegTesting')
}

useTestFixtures()
useTestFixtures(project: ':languageJava')
useTestFixtures(project: ':messaging')
useTestFixtures(project: ":logging")

integTestTasks.all {
    maxParallelForks = Math.min(3, rootProject.maxParallelForks)
}

task configureJar {
    doLast {
        jar.manifest.mainAttributes('Class-Path': "${project(':core').jar.archivePath.name} ${project(':baseServices').jar.archivePath.name}")
    }
}

jar {
    dependsOn configureJar
    manifest.mainAttributes('Main-Class': "org.gradle.launcher.GradleMain")
}

task startScripts(type: GradleStartScriptGenerator) {
    startScriptsDir = new File("$buildDir/startScripts")
    launcherJar = jar.outputs.files
}

daemonIntegTest {
    //those tests are always using the daemon, they use exclusive daemons and they are a part of a regular check-in build anyway.
    //since they are using exclusive daemons they don't contribute to the daemonIntegTest stress/load test.
    //excluding to avoid unnecessary re-running and stealing resources.
    exclude "org/gradle/launcher/daemon/**/*"
}

useClassycle()

verifyTestFilesCleanup.errorWhenNotEmpty = false

